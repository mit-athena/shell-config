#!/usr/bin/tcsh
# Global cshrc file for Debathena machines

setenv HOSTTYPE "`/bin/machtype`"
setenv ATHENA_SYS `/bin/machtype -S`
if ( $ATHENA_SYS == "" ) then
  setenv ATHENA_SYS @sys
endif
setenv ATHENA_SYS_COMPAT `/bin/machtype -C`
alias debathena_fsid true
if ( $?DEBATHENA_HOME_TYPE ) then
    if ( "$DEBATHENA_HOME_TYPE" == afs ) then
	if ( -x "/bin/fsid" ) then
	    alias debathena_fsid 'fsid -a'
	else
	    alias debathena_fsid 'aklog -p "$HOME"'
	endif
    endif
endif

alias renew 'set KINIT_ARGS=""; kinit -4 </dev/null >& /dev/null; if ($? != 3) set KINIT_ARGS="-45"; kinit $KINIT_ARGS "$ATHENA_USER" && debathena_fsid && aklog'

set add_flags
alias add 'eval `/bin/attach -Padd $add_flags \!:*`'
